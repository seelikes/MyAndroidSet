apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion version_compile_sdk as int

    defaultConfig {
        minSdkVersion version_min_sdk as int
        targetSdkVersion version_target_sdk as int
        versionCode 100
        versionName "1.0.00"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$version_support"
    implementation "com.android.support.constraint:constraint-layout:$version_constraint_layout"
    testImplementation "junit:junit:$version_junit"
    androidTestImplementation "com.android.support.test:runner:$version_runner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$version_espresso_core"
    api "com.jakewharton.timber:timber:$version_timber"
    api "com.java.lib:oil:$version_oil"
    api "io.reactivex.rxjava2:rxandroid:$version_rxandroid"
    api "io.reactivex.rxjava2:rxjava:$version_rxjava"
    api "me.yatoooon:screenadaptation:$version_screenadaptation"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options.linkSource true
    options.encoding "UTF-8"
    classpath += files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = "javadoc"
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = "sources"
    from android.sourceSets.main.java.sourceFiles
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "com.github.seelikes.android"
            artifactId "mvvm-base"
            version "1.0.00"
            artifact androidJavadocsJar
            artifact androidSourcesJar
            artifact bundleReleaseAar
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                wrapIntoDependencies(dependencies, configurations.api, "compile")
                wrapIntoDependencies(dependencies, configurations.implementation, "provided")
                wrapIntoDependencies(dependencies, configurations.testImplementation, "test")
                wrapIntoDependencies(dependencies, configurations.androidTestImplementation, "test")
            }
        }
    }
    repositories {
        maven {
            url project.property('deploy.release.url')
            credentials {
                username project.property("deploy.release.user.name")
                password project.property("deploy.release.user.password")
            }
        }
    }
}
